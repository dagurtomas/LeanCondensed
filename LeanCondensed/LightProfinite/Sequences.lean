/-
Copyright (c) 2024 Dagur Asgeirsson. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Dagur Asgeirsson
-/
import Mathlib.Topology.Compactification.OnePoint
import Mathlib.Topology.Category.LightProfinite.Basic
/-!

# The light profinite set classifying convergent sequences

This files defines the light profinite set `â„•âˆª{âˆ}`, defined as the one point compactification of
`â„•`.
-/

open CategoryTheory TopologicalSpace OnePoint

namespace OnePoint

open Filter Topology Set

protected lemma Â«forallÂ» {X : Type*} {p : OnePoint X â†’ Prop} :
    (âˆ€ (x : OnePoint X), p x) â†” p âˆ âˆ§ âˆ€ (x : X), p (some x) :=
  Option.forall

lemma continuous_iff {X Y : Type*} [TopologicalSpace X]
    [TopologicalSpace Y] (f : OnePoint X â†’ Y) : Continuous f â†”
    Tendsto (fun x : X â†¦ f x) (coclosedCompact X) (ğ“ (f âˆ)) âˆ§ Continuous (fun x : X â†¦ f x) := by
  simp_rw [continuous_iff_continuousAt, OnePoint.forall, continuousAt_coe, continuousAt_infty']
  rfl

lemma continuous_iff_of_discrete {X Y : Type*} [TopologicalSpace X] [TopologicalSpace Y]
    [DiscreteTopology X] (f : OnePoint X â†’ Y) :
    Continuous f â†” Tendsto (fun x : X â†¦ f x) cofinite (ğ“ (f âˆ)) := by
  simp [continuous_iff, cocompact_eq_cofinite, continuous_of_discreteTopology]

lemma continuous_iff_of_nat {Y : Type*} [TopologicalSpace Y] (f : OnePoint â„• â†’ Y) :
    Continuous f â†” Tendsto (fun x : â„• â†¦ f x) atTop (ğ“ (f âˆ)) := by
  rw [continuous_iff_of_discrete, Nat.cofinite_eq_atTop]

instance (X : Type*) [TopologicalSpace X] [DiscreteTopology X] :
    TotallySeparatedSpace (OnePoint X) where
  isTotallySeparated_univ x _ y _ hxy := by
    cases x with
    | none =>
      refine âŸ¨{y}á¶œ, {y}, isOpen_compl_singleton, ?_, hxy, rfl, (compl_union_self _).symm.subset,
        disjoint_compl_leftâŸ©
      rw [OnePoint.isOpen_iff_of_not_mem]
      exacts [isOpen_discrete _, hxy]
    | some val =>
      refine âŸ¨{some val}, {some val}á¶œ, ?_, isOpen_compl_singleton, rfl, hxy.symm, by simp,
        disjoint_compl_rightâŸ©
      rw [OnePoint.isOpen_iff_of_not_mem]
      exacts [isOpen_discrete _, (Option.some_ne_none val).symm]

instance (X : Type*) [TopologicalSpace X] [c : CompactSpace X] [MetrizableSpace X] :
    SecondCountableTopology X := by
  obtain âŸ¨_, hâŸ© := MetrizableSpace.exists_metric (X := X)
  rw [â† h] at c âŠ¢
  infer_instance

end OnePoint

namespace LightProfinite

/-- The continuous map from `â„•âˆª{âˆ}` to `â„`Â sending `n`Â to `1/(n+1)` and `âˆ` to `0`. -/
noncomputable def natUnionInftyEmbedding : C(OnePoint â„•, â„) where
  toFun
    | âˆ => 0
    | OnePoint.some n => 1 / (n+1 : â„)
  continuous_toFun := OnePoint.continuous_iff_of_nat _ |>.mpr
    tendsto_one_div_add_atTop_nhds_zero_nat

/--
The continuous map from `â„•âˆª{âˆ}` to `â„`Â sending `n`Â to `1/(n+1)` and `âˆ` to `0` is a closed
embedding.
-/
lemma closedEmbedding_natUnionInftyEmbedding : ClosedEmbedding natUnionInftyEmbedding := by
  refine closedEmbedding_of_continuous_injective_closed
    natUnionInftyEmbedding.continuous ?_ ?_
  Â· rintro (_|n) (_|m) h
    Â· rfl
    Â· simp only [natUnionInftyEmbedding, one_div, ContinuousMap.coe_mk, zero_eq_inv] at h
      rw [â† Nat.cast_one, â† Nat.cast_add, eq_comm, Nat.cast_eq_zero] at h
      simp at h
    Â· simp only [natUnionInftyEmbedding, one_div, ContinuousMap.coe_mk, inv_eq_zero] at h
      rw [â† Nat.cast_one, â† Nat.cast_add, Nat.cast_eq_zero] at h
      simp at h
    Â· simp only [natUnionInftyEmbedding, one_div, ContinuousMap.coe_mk, inv_inj, add_left_inj,
        Nat.cast_inj] at h
      rw [h]
  Â· exact fun _ hC => (hC.isCompact.image natUnionInftyEmbedding.continuous).isClosed

instance : MetrizableSpace (OnePoint â„•) := closedEmbedding_natUnionInftyEmbedding.metrizableSpace

/-- The one point compactification of the natural numbers as a light profinite set. -/
abbrev NatUnionInfty : LightProfinite := of (OnePoint â„•)

@[inherit_doc]
scoped notation "â„•âˆª{âˆ}" => NatUnionInfty

instance : Coe â„• â„•âˆª{âˆ} := optionCoe

open Filter Topology

lemma continuous_iff_convergent {Y : Type*} [TopologicalSpace Y] (f : â„•âˆª{âˆ} â†’ Y) :
    Continuous f â†” Tendsto (fun x : â„• â†¦ f x) atTop (ğ“ (f âˆ)) :=
  continuous_iff_of_nat f

end LightProfinite
