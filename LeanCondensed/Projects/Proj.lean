/-
Copyright (c) 2025 Jonas van der Schaaf. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jonas van der Schaaf
-/
import Mathlib.Condensed.Light.InternallyProjective
import LeanCondensed.Projects.LightProfiniteInjective
import LeanCondensed.Projects.PreservesCoprod
import LeanCondensed.Projects.Epi
import LeanCondensed.Mathlib.CategoryTheory.Countable
import LeanCondensed.Mathlib.Topology.Category.CompHausLike.Limits

open CategoryTheory Functor Opposite LightProfinite OnePoint Limits LightCondensed
  MonoidalCategory MonoidalClosed WalkingParallelPair WalkingParallelPairHom
  CartesianMonoidalCategory Topology

universe u

section

variable {X Y : LightProfinite} (y : Y) (f : X âŸ¶ Y)

def fibre : LightProfinite :=
  haveI : CompactSpace (f â»Â¹' {y}) :=
    isCompact_iff_compactSpace.mp (IsClosed.preimage (by fun_prop) isClosed_singleton).isCompact
  CompHausLike.of _ (f â»Â¹' {y})

def fibre_incl : fibre y f âŸ¶ X := âŸ¨{ toFun := Subtype.val }âŸ©

variable {Z : LightProfinite} {f : X âŸ¶ Z} {g : Y âŸ¶ Z}

instance [h : Epi f] : Epi (CompHausLike.pullback.snd f g) := by
  rw [LightProfinite.epi_iff_surjective] at h âŠ¢
  intro y
  obtain âŸ¨x, hxâŸ© := h (g y)
  exact âŸ¨âŸ¨âŸ¨x, yâŸ©, hxâŸ©, rflâŸ©

end

variable (R : Type) [CommRing R]

@[simps!]
def CategoryTheory.Limits.parallelPairNatTrans {C : Type*} [Category C]
    {F G : WalkingParallelPair â¥¤ C} (f0 : F.obj zero âŸ¶ G.obj zero)
    (f1 : F.obj one âŸ¶ G.obj one) (wl : F.map left â‰« f1 = f0 â‰« G.map left)
    (wr : F.map right â‰« f1 = f0 â‰« G.map right) : F âŸ¶ G where
  app | zero => f0 | one => f1
  naturality := by rintro _ _ âŸ¨_âŸ© <;> simp [wl, wr]

def fibresOfOption {S T X : Type*} (Ï€ : T â†’ S Ã— Option X) (Ïƒ : Option X â†’ S â†’ T) : Set T :=
  {t : T | (Ï€ t).2 = none} âˆª (â‹ƒ (x : X), Set.range (Ïƒ x))

lemma fibresOfOption_closed {S T X : Type*} [TopologicalSpace S] [TopologicalSpace T]
    [TopologicalSpace X] [DiscreteTopology X] [T2Space T] [CompactSpace S]
    (Ï€ : T â†’ S Ã— OnePoint X) (hÏ€ : Continuous Ï€)
    (Ïƒ : Option X â†’ S â†’ T) (hÏƒ : âˆ€ x, Continuous (Ïƒ x))
    (hÏƒ' : âˆ€ (x : Option X) (s : S), (Ï€ (Ïƒ x s)).2 = x) :
    IsClosed (fibresOfOption Ï€ Ïƒ) := IsClosed.mk <| by
  have : IsOpen (â‹ƒ i, (Set.range (Ïƒ (Option.some i)))á¶œ âˆ© (Prod.snd âˆ˜ Ï€) â»Â¹' {(i : OnePoint X)}) := by
    refine isOpen_iUnion fun i â†¦ IsOpen.inter ?_ ?_
    Â· simpa using IsCompact.isClosed (isCompact_range (hÏƒ i))
    Â· refine .preimage (continuous_snd.comp hÏ€) âŸ¨fun h â†¦ by simp_all, ?_âŸ©
      convert isOpen_discrete {i}
      aesop
  convert this
  ext x
  simp only [fibresOfOption, Set.compl_union, Set.compl_iUnion, Set.mem_inter_iff,
    Set.mem_compl_iff, Set.mem_setOf_eq, Set.mem_iInter, Set.mem_range, not_exists, Set.mem_iUnion,
    Set.mem_preimage, Function.comp_apply, Set.mem_singleton_iff]
  refine âŸ¨fun âŸ¨hâ‚, hâ‚‚âŸ© â†¦ ?_, fun âŸ¨n, hn, hn'âŸ© â†¦ ?_âŸ©
  Â· obtain âŸ¨n, hnâŸ© := Option.ne_none_iff_exists'.mp hâ‚
    exact âŸ¨n, hâ‚‚ n, hnâŸ©
  Â· refine âŸ¨by simpa [hn'] using Option.isSome_iff_ne_none.mp rfl, fun i s h â†¦ hn s ?_âŸ©
    rw [â† h, hÏƒ'] at hn'
    rw [â† h, Option.some_injective _ hn'.symm]

def Ï€_r {S T X : Type*} (Ï€ : T â†’ S Ã— Option X) (Ïƒ : Option X â†’ S â†’ T) :
    fibresOfOption Ï€ Ïƒ â†’ S Ã— Option X :=
  fun x â†¦ Ï€ x

def fibreInclGeneral {S T : Type*} (y : T) (f : S â†’ T) : f â»Â¹' {y} â†’ S := fun x â†¦ x

def fibreIncl {S T X : Type*} (Ï€ : T â†’ S Ã— Option X) (Ïƒ : Option X â†’ S â†’ T) :
    (Prod.snd âˆ˜ Ï€_r Ï€ Ïƒ) â»Â¹' {none} â†’ fibresOfOption Ï€ Ïƒ :=
  fibreInclGeneral none (Prod.snd âˆ˜ Ï€_r Ï€ Ïƒ)

lemma fibresOfOption_surjective {S T X : Type*}
    (Ï€ : T â†’ S Ã— Option X) (hÏ€ : Ï€.Surjective) (Ïƒ : Option X â†’ S â†’ T)
    (hÏƒ : âˆ€ (x : Option X) s, (Ï€ (Ïƒ x s)).1 = s)
    (hÏƒ' : âˆ€ (x : Option  X) (s : S), (Ï€ (Ïƒ x s)).2 = x) :
    (fun (x : fibresOfOption Ï€ Ïƒ) â†¦ Ï€ x).Surjective := by
  rintro âŸ¨s, (rfl | x)âŸ©
  Â· obtain âŸ¨y, hyâŸ© := hÏ€ (s, none)
    exact âŸ¨âŸ¨y, by simp [fibresOfOption, hy]âŸ©, hyâŸ©
  Â· exact âŸ¨âŸ¨Ïƒ x s, by simp [fibresOfOption]âŸ©, Prod.ext (by grind) (by grind)âŸ©

abbrev smartCoverToFun {S T X Y : Type*} (i : Y â†’ T) (Ï€ : T â†’ S Ã— Option X) :
    T âŠ• {xy : Y Ã— Y // Ï€ (i xy.1) = Ï€ (i xy.2)} â†’ {xy : T Ã— T // Ï€ xy.1 = Ï€ xy.2} :=
  Sum.elim (fun t â†¦ âŸ¨(t, t), rflâŸ©) (fun xy â†¦ âŸ¨(i xy.val.1, i xy.val.2), xy.propâŸ©)

lemma smartCoverToFun_surjective {S T X : Type*} (Ï€ : T â†’ S Ã— Option X) (Ïƒ : Option X â†’ S â†’ T)
    (hÏƒ : âˆ€ (x : Option X) s, (Ï€ (Ïƒ x s)).1 = s) (hÏƒ' : âˆ€ (x : Option X) (s : S), (Ï€ (Ïƒ x s)).2 = x) :
    Function.Surjective (smartCoverToFun (fibreIncl Ï€ Ïƒ) (Ï€_r Ï€ Ïƒ)) := by
  intro âŸ¨âŸ¨t, t'âŸ©, htâŸ©
  dsimp [Ï€_r] at ht
  have hht (x : X) (t : fibresOfOption Ï€ Ïƒ) (hn : x = (Ï€ t).2) : Ïƒ x (Ï€ t).1 = t := by
    have ht' := t.prop
    simp only [fibresOfOption, Set.mem_union] at ht'
    obtain (ht' | ht') := ht'
    Â· have ht' : (Ï€ t).2 = none := by dsimp at ht'; simp [â† ht']
      simp_all
    Â· simp only [Set.mem_iUnion, Set.mem_range] at ht'
      obtain âŸ¨m, s, ht'âŸ© := ht'
      rw [â† ht', hÏƒ m s, â† hÏƒ' m s, hn, â† ht']
  by_cases h : (Ï€ t).2 = none
  Â· exact âŸ¨Sum.inr âŸ¨(âŸ¨t, by grind [Ï€_r]âŸ©, âŸ¨t', by grind [Ï€_r]âŸ©),
      by simp [fibreIncl, fibreInclGeneral, Ï€_r, ht]âŸ©,
      by simp [smartCoverToFun, fibreIncl, fibreInclGeneral]âŸ©
  Â· rw [â† ne_eq, Option.ne_none_iff_exists'] at h
    obtain âŸ¨n, hnâŸ© := h
    refine âŸ¨Sum.inl âŸ¨Ïƒ n (Ï€_r Ï€ Ïƒ t).1, by simp [fibresOfOption]âŸ©, ?_âŸ©
    simp only [Sum.elim_inl, Subtype.mk.injEq, Prod.mk.injEq]
    refine âŸ¨Subtype.ext <| hht _ _ hn.symm, ?_âŸ©
    rw [ht] at hn
    simpa [Ï€_r, ht] using Subtype.ext <| hht _ _ hn.symm

abbrev smartCoverNew {S T : LightProfinite} (Ï€ : T âŸ¶ S âŠ— â„•âˆª{âˆž}) :
    (CompHausLike.of _ (T âŠ• (CompHausLike.pullback (fibre_incl âˆž (Ï€ â‰« snd S â„•âˆª{âˆž}) â‰« Ï€)
      (fibre_incl âˆž (Ï€ â‰« snd S â„•âˆª{âˆž}) â‰« Ï€)))) âŸ¶ CompHausLike.pullback Ï€ Ï€ := âŸ¨{
  toFun := smartCoverToFun _ _
  continuous_toFun := by simpa using by fun_prop }âŸ©

def sectionOfFibreIncl {S T X : Type*} (Ï€ : T â†’ S Ã— Option X) (Ïƒ : Option X â†’ S â†’ T)
    (hÏƒ' : âˆ€ (x : Option X) (s : S), (Ï€ (Ïƒ x s)).2 = x) : S â†’ (Prod.snd âˆ˜ Ï€_r Ï€ Ïƒ) â»Â¹' {none} :=
  fun s â†¦ âŸ¨âŸ¨Ïƒ none s, by simp [fibresOfOption, hÏƒ']âŸ©, by simp [hÏƒ', Ï€_r]âŸ©

lemma refinedCover {S T : LightProfinite} (Ï€ : T âŸ¶ S âŠ— â„•âˆª{âˆž}) [Epi Ï€] :
    âˆƒ (S' T' : LightProfinite) (y' : S' âŸ¶ S) (Ï€' : T' âŸ¶ S' âŠ— â„•âˆª{âˆž}) (g' : T' âŸ¶ T),
      Epi Ï€' âˆ§ Epi y' âˆ§ Ï€' â‰« (y' â–· â„•âˆª{âˆž}) = g' â‰« Ï€ âˆ§
        IsSplitEpi (fibre_incl âˆž (Ï€' â‰« snd S' â„•âˆª{âˆž}) â‰« Ï€' â‰« fst S' â„•âˆª{âˆž}) âˆ§
          Epi (smartCoverNew Ï€') := by
  have : Countable â„•âˆª{âˆž} := inferInstanceAs (Countable <| Option _)
  have : CompactSpace
      {x : (n : â„•âˆª{âˆž}) â†’ fibre n (Ï€ â‰« snd _ _) | âˆ€ n m, (Ï€ (x n).val).1 = (Ï€ (x m).val).1} := by
    rw [â† isCompact_iff_compactSpace, show
      {x : (n : â„•âˆª{âˆž}) â†’ fibre n (Ï€ â‰« snd _ _) | âˆ€ n m, (Ï€ (x n).val).1 = (Ï€ (x m).val).1} =
      â‹‚ (n : â„•âˆª{âˆž}) (m : â„•âˆª{âˆž}), {x | (Ï€ (x n).val).1 = (Ï€ (x m).val).1} by aesop]
    refine (isClosed_iInter fun n â†¦ isClosed_iInter fun m â†¦ isClosed_eq ?_ ?_).isCompact
    all_goals exact continuous_fst.comp (Continuous.comp (by fun_prop) (continuous_subtype_val.comp
        ((continuous_apply _).comp (by fun_prop))))
  let S' : LightProfinite := LightProfinite.of
    {x : (n : â„•âˆª{âˆž}) â†’ fibre n (Ï€ â‰« snd _ _) | âˆ€ n m, (Ï€ (x n).val).1 = (Ï€ (x m).val).1}
  let S'Ï€ (n : â„•âˆª{âˆž}) : S' âŸ¶ fibre n (Ï€ â‰« snd _ _) :=
    âŸ¨{ toFun x := x.val n, continuous_toFun := by refine (continuous_apply _).comp ?_; fun_prop }âŸ©
  let y' : S' âŸ¶ S := CompHausLike.ofHom _ {
    toFun x := (Ï€ (x.val none).val).1
    continuous_toFun := continuous_fst.comp <| Continuous.comp (by fun_prop) <|
      continuous_subtype_val.comp <| (continuous_apply _).comp <| by fun_prop }
  let Ttilde := CompHausLike.pullback Ï€ (y' â–· â„•âˆª{âˆž})
  let Ï€_tilde : Ttilde âŸ¶ S' âŠ— â„•âˆª{âˆž} := CompHausLike.pullback.snd _ _
  let Ïƒ' : â„•âˆª{âˆž} â†’ S' â†’ Ttilde := fun n â†¦ CompHausLike.pullback.lift _ _
    (S'Ï€ n â‰« fibre_incl _ _) (lift (ðŸ™ S') (CompHausLike.const _ n)) <| by
      apply CartesianMonoidalCategory.hom_ext
      Â· ext x; exact x.prop n none
      Â· ext x; exact (x.val n).prop
  have hÏƒ (x : â„•âˆª{âˆž}) (s : S') : (Ï€_tilde (Ïƒ' x s)).1 = s := by simp [Ïƒ', Ï€_tilde]; rfl
  have hÏƒ' (x : â„•âˆª{âˆž}) (s : S') : (Ï€_tilde (Ïƒ' x s)).2 = x := by simp [Ïƒ', Ï€_tilde]; rfl
  have : CompactSpace (fibresOfOption Ï€_tilde Ïƒ') := isCompact_iff_compactSpace.mp
    (fibresOfOption_closed Ï€_tilde (by fun_prop) Ïƒ' (by fun_prop) hÏƒ').isCompact
  refine âŸ¨S', LightProfinite.of (fibresOfOption Ï€_tilde Ïƒ'), y',
    âŸ¨âŸ¨Subtype.val, by fun_propâŸ©âŸ© â‰« Ï€_tilde,
    âŸ¨âŸ¨Subtype.val, by fun_propâŸ©âŸ© â‰« CompHausLike.pullback.fst _ _, ?_, ?_, ?_, ?_, ?_âŸ©
  Â· rw [LightProfinite.epi_iff_surjective]
    refine fibresOfOption_surjective _ ?_ _ hÏƒ hÏƒ'
    rw [â† LightProfinite.epi_iff_surjective]
    dsimp [Ï€_tilde]
    infer_instance
  Â· rw [LightProfinite.epi_iff_surjective]
    intro y
    have : Function.Surjective Ï€ := by rwa [â† LightProfinite.epi_iff_surjective]
    let p (s : S) (n : â„•âˆª{âˆž}) : T := (this (s, n)).choose
    have hp (s : S) (n : â„•âˆª{âˆž}) : Ï€ (p s n) = (s, n) := (this (s, n)).choose_spec
    refine âŸ¨âŸ¨fun n â†¦ âŸ¨p y n, ?_âŸ©, ?_âŸ©, ?_âŸ©
    Â· simp [hp]; rfl
    Â· simp [hp]
    Â· simp [y', hp]
  Â· simp [Ï€_tilde, CompHausLike.pullback.condition]
  Â· exact âŸ¨âŸ¨âŸ¨sectionOfFibreIncl Ï€_tilde Ïƒ' hÏƒ', .subtype_mk (.subtype_mk (by fun_prop) _) _âŸ©âŸ©, rflâŸ©
  Â· rw [LightProfinite.epi_iff_surjective]
    exact smartCoverToFun_surjective _ _ hÏƒ hÏƒ'

private lemma comm_sq {X Y : LightCondMod R} (p : X âŸ¶ Y) [hp : Epi p] {S : LightProfinite}
    (f : (free R).obj (S).toCondensed âŸ¶ Y) :
      âˆƒ (T : LightProfinite) (Ï€ : T âŸ¶ S) (g : ((free R).obj T.toCondensed) âŸ¶ X),
        Epi Ï€ âˆ§ (lightProfiniteToLightCondSet â‹™ (free R)).map Ï€ â‰« f = g â‰« p := by
  have : Epi ((LightCondensed.forget _).map p) := inferInstance
  rw [LightCondSet.epi_iff_locallySurjective_on_lightProfinite] at this
  let y : Y.val.obj (op S) := (coherentTopology LightProfinite).yonedaEquiv <|
    (Adjunction.homEquiv (freeForgetAdjunction R) (S).toCondensed Y f)
  obtain âŸ¨T, Ï€, hÏ€, x, hxâŸ© := this S y
  let g : (free R).obj T.toCondensed âŸ¶ X :=
    ((freeForgetAdjunction R).homEquiv T.toCondensed X).symm
      ((coherentTopology LightProfinite).yonedaEquiv.symm x)
  refine âŸ¨T, Ï€, g, (LightProfinite.epi_iff_surjective Ï€).mpr hÏ€, ?_âŸ©
  rw [Functor.comp_map, â† Adjunction.homEquiv_naturality_left_square_iff (freeForgetAdjunction R),
    Equiv.apply_symm_apply, Sheaf.hom_ext_iff,
    (coherentTopology LightProfinite).yonedaEquiv_symm_naturality_right, hx,
    (coherentTopology LightProfinite).map_yonedaEquiv',
    â† (coherentTopology LightProfinite).yonedaEquiv_symm_naturality_right]
  rfl

instance : IsLeftAdjoint (free R) := âŸ¨_, âŸ¨LightCondensed.freeForgetAdjunction RâŸ©âŸ©

noncomputable def hc {S T : LightProfinite} (Ï€ : T âŸ¶ S) [Epi Ï€] :
    IsColimit ((free R).mapCocone (regular Ï€)) :=
  isColimitOfPreserves _ (explicitRegularIsColimit _)

noncomputable def c {X : LightCondMod R} {S T : LightProfinite} (Ï€ : T âŸ¶ (S âŠ— â„•âˆª{âˆž}))
    [Epi ((lightProfiniteToLightCondSet â‹™ (free R)).map <| smartCoverNew Ï€)]
    (g : ((lightProfiniteToLightCondSet â‹™ free R).obj T) âŸ¶ X)
    (r_inf : T âŸ¶ (fibre âˆž (Ï€ â‰« snd _ _))) (Ïƒ : S âŸ¶ (fibre âˆž (Ï€ â‰« snd _ _)))
    (hr : fibre_incl âˆž (Ï€ â‰« snd _ _) â‰« r_inf = ðŸ™ (fibre âˆž (Ï€ â‰« snd _ _))) :
    Cocone ((parallelPair (lightProfiniteToLightCondSet.map (CompHausLike.pullback.fst Ï€ Ï€))
      (lightProfiniteToLightCondSet.map (CompHausLike.pullback.snd Ï€ Ï€))) â‹™ (free R)) where
  pt := X
  Î¹ :=  by
    let g_tilde : (lightProfiniteToLightCondSet â‹™ (free R)).obj T âŸ¶ X :=
      g -
        (lightProfiniteToLightCondSet â‹™ (free R)).map (r_inf â‰« fibre_incl âˆž (Ï€ â‰« snd _ _)) â‰« g +
        (lightProfiniteToLightCondSet â‹™ (free R)).map
          (r_inf â‰« fibre_incl âˆž (Ï€ â‰« snd _ _) â‰« Ï€ â‰« fst _ _ â‰« Ïƒ â‰« fibre_incl âˆž (Ï€ â‰« snd _ _)) â‰« g
    refine parallelPairNatTrans (_ â‰« g_tilde) g_tilde ?_ rfl
    rw [â† cancel_epi ((lightProfiniteToLightCondSet â‹™ (free R)).map <| smartCoverNew Ï€)]
    apply (isColimitOfPreserves (lightProfiniteToLightCondSet â‹™ (free R))
        (CompHausLike.coprod.isColimit _ _)).hom_ext
    rintro âŸ¨âŸ¨âŸ©âŸ©
    Â· simp [â† Functor.map_comp_assoc, â† Functor.map_comp]
      rfl
    Â· simp only [comp_obj, pair_obj_right, const_obj_obj, mapCocone_pt, BinaryCofan.mk_pt,
        mapCocone_Î¹_app, BinaryCofan.mk_inr, Functor.comp_map, parallelPair_obj_zero,
        parallelPair_obj_one, parallelPair_map_left, â† map_comp_assoc, â† Functor.map_comp,
        parallelPair_map_right, const_obj_map, Category.comp_id]
      have : smartCoverNew Ï€ =
          CompHausLike.coprod.desc (CompHausLike.pullback.lift _ _ (ðŸ™ T) (ðŸ™ T) (by simp))
            (CompHausLike.pullback.lift _ _ ((CompHausLike.pullback.fst _ _) â‰« fibre_incl _ _)
              ((CompHausLike.pullback.snd _ _) â‰« fibre_incl _ _)
              (by simp [CompHausLike.pullback.condition])) := rfl
      simp only [this, CompHausLike.coprod.inr_desc_assoc, CompHausLike.pullback.lift_fst, comp_obj,
        Functor.comp_map, Preadditive.comp_add, Preadditive.comp_sub, â† map_comp_assoc,
        â† Functor.map_comp, Category.assoc, CompHausLike.pullback.lift_snd, g_tilde]
      simp only [â† Functor.comp_map, â† Category.assoc, hr, Category.id_comp, sub_self, zero_add]
      simp [CompHausLike.pullback.condition]

instance (X Y : LightProfinite.{u}) [Nonempty X] : Epi (snd X Y) := by
  rw [LightProfinite.epi_iff_surjective]
  exact fun y â†¦ âŸ¨âŸ¨Nonempty.some inferInstance, yâŸ©, rflâŸ©

theorem internallyProjective_â„•infty : InternallyProjective ((free R).obj (â„•âˆª{âˆž}).toCondensed) := by
  rw [free_lightProfinite_internallyProjective_iff_tensor_condition' R â„•âˆª{âˆž}]
  intro X Y p hp S f
  obtain âŸ¨T, Ï€, g, hÏ€, commâŸ© := comm_sq R p f
  obtain âŸ¨S', T', y', Ï€', g', hÏ€', hy', comp, âŸ¨âŸ¨splitâŸ©âŸ©, epiâŸ© := refinedCover Ï€
  refine âŸ¨S', y', ?_âŸ©
  by_cases hS' : Nonempty S'
  Â· have : Mono (fibre_incl âˆž (Ï€' â‰« snd _ _)) := by
      rw [CompHausLike.mono_iff_injective]
      simp [fibre_incl]
      exact Subtype.val_injective
    have : Nonempty (fibre âˆž (Ï€' â‰« snd _ _)) := by
      have : Epi (Ï€' â‰« snd S' â„•âˆª{âˆž}) := inferInstance
      obtain âŸ¨x, hxâŸ© := (LightProfinite.epi_iff_surjective _).mp this âˆž
      refine âŸ¨x, by simpa using hxâŸ©
    obtain âŸ¨r_inf, hrâŸ© := Injective.factors (ðŸ™ _) (fibre_incl âˆž (Ï€' â‰« snd _ _))
    refine âŸ¨(LightProfinite.epi_iff_surjective _).mp inferInstance,
      (hc R Ï€').desc (c R Ï€' ((lightProfiniteToLightCondSet â‹™ (free R)).map g' â‰« g)
      r_inf split.section_ hr), ?_âŸ©
    rw [â† cancel_epi ((lightProfiniteToLightCondSet â‹™ (free R)).map Ï€'),
      â† Functor.comp_map, â† Functor.map_comp_assoc]
    change _ = (((free R).mapCocone _).Î¹.app one â‰« (hc R Ï€').desc (c R Ï€' _ r_inf split.section_ hr)) â‰« p
    rw [(hc R Ï€').fac]
    -- simp? [c, â† comm]:
    simp only [comp_obj, parallelPair_obj_one, Functor.comp_map, Functor.map_comp, Category.assoc,
      c, parallelPair_obj_zero, const_obj_obj, parallelPair_map_right, Lean.Elab.WF.paramLet,
      Preadditive.comp_add, Preadditive.comp_sub, parallelPairNatTrans_app, Preadditive.add_comp,
      Preadditive.sub_comp, â† comm]
    simp only [â† Functor.map_comp, â† Functor.comp_map, â† Category.assoc, â† comp]
    symm
    rw [sub_add, sub_eq_self, sub_eq_zero]
    simp only [Category.assoc]
    have : fibre_incl âˆž (Ï€' â‰« snd _ _) â‰« Ï€' = fibre_incl âˆž (Ï€' â‰« snd _ _) â‰« Ï€' â‰« fst _ _ â‰«
        lift (ðŸ™ _) (CompHausLike.const S' (âˆž : â„•âˆª{âˆž})) := by
      apply CartesianMonoidalCategory.hom_ext
      Â· simp [fibre_incl]
      Â· ext a
        exact a.prop
    rw [reassoc_of% this, reassoc_of% split.id]
  Â· have hh : IsEmpty (S' âŠ— â„•âˆª{âˆž}) := { false a := IsEmpty.elim (by simpa using hS') (fst S' _ a) }
    have : IsIso Ï€' := âŸ¨CompHausLike.ofHom _ âŸ¨(hh.elim Â·), continuous_of_const <| by aesopâŸ©,
      by ext x; exact hh.elim (Ï€' x), by ext x; all_goals exact hh.elim xâŸ©
    refine âŸ¨(LightProfinite.epi_iff_surjective _).mp inferInstance,
      (lightProfiniteToLightCondSet â‹™ (free R)).map (inv Ï€' â‰« g') â‰« g, ?_âŸ©
    simp only [comp_obj, Functor.comp_map, Functor.map_comp, Functor.map_inv,
      Category.assoc, â† comm, â† cancel_epi ((lightProfiniteToLightCondSet â‹™ (free R)).map Ï€'),
      IsIso.hom_inv_id_assoc]
    simp [â† Category.assoc, â† Functor.map_comp, â† comp]
