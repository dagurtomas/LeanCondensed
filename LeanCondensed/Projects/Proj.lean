/-
Copyright (c) 2025 Jonas van der Schaaf. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jonas van der Schaaf
-/
import Mathlib.Condensed.Light.InternallyProjective
import Mathlib.Topology.FiberPartition
import LeanCondensed.Projects.LightProfiniteInjective
import LeanCondensed.Projects.PreservesCoprod
import LeanCondensed.Projects.Epi
import LeanCondensed.Mathlib.CategoryTheory.Countable
import LeanCondensed.Mathlib.Topology.Category.CompHausLike.Limits

open CategoryTheory Functor Opposite LightProfinite OnePoint Limits LightCondensed
  MonoidalCategory MonoidalClosed WalkingParallelPair WalkingParallelPairHom
  CartesianMonoidalCategory Topology

universe u

variable (R : Type) [CommRing R]

-- TODO: put in the file about explicit limits in `CompHausLike`
instance {X Y Z : LightProfinite} (f : X ‚ü∂ Z) (g : Y ‚ü∂ Z) [h : Epi f] :
    Epi (CompHausLike.pullback.snd f g) := by
  rw [LightProfinite.epi_iff_surjective] at h ‚ä¢
  intro y
  obtain ‚ü®x, hx‚ü© := h (g y)
  exact ‚ü®‚ü®‚ü®x, y‚ü©, hx‚ü©, rfl‚ü©

-- TODO: rename and put in the file about epimorphisms in light condensed sets/modules.
lemma comm_sq {X Y : LightCondMod R} (p : X ‚ü∂ Y) [hp : Epi p] {S : LightProfinite}
    (f : (free R).obj (S).toCondensed ‚ü∂ Y) :
      ‚àÉ (T : LightProfinite) (œÄ : T ‚ü∂ S) (g : ((free R).obj T.toCondensed) ‚ü∂ X),
        Epi œÄ ‚àß (lightProfiniteToLightCondSet ‚ãô (free R)).map œÄ ‚â´ f = g ‚â´ p := by
  have : Epi ((LightCondensed.forget _).map p) := inferInstance
  rw [LightCondSet.epi_iff_locallySurjective_on_lightProfinite] at this
  obtain ‚ü®T, œÄ, hœÄ, x, hx‚ü© := this S <| (coherentTopology LightProfinite).yonedaEquiv <|
    (freeForgetAdjunction R).homEquiv S.toCondensed Y f
  refine ‚ü®T, œÄ, ((freeForgetAdjunction R).homEquiv T.toCondensed X).symm
    ((coherentTopology LightProfinite).yonedaEquiv.symm x),
    (LightProfinite.epi_iff_surjective œÄ).mpr hœÄ, ?_‚ü©
  rw [Functor.comp_map, ‚Üê Adjunction.homEquiv_naturality_left_square_iff (freeForgetAdjunction R),
    Sheaf.hom_ext_iff, Equiv.apply_symm_apply,
    GrothendieckTopology.yonedaEquiv_symm_naturality_right, hx,
    GrothendieckTopology.map_yonedaEquiv', ‚Üê GrothendieckTopology.yonedaEquiv_symm_naturality_right]
  rfl

-- TODO: put in the file where the adjunction is defined
instance : IsLeftAdjoint (free R) := ‚ü®_, ‚ü®LightCondensed.freeForgetAdjunction R‚ü©‚ü©

-- TODO: put in the file that defines `‚Ñï‚à™{‚àû}`
instance : Countable ‚Ñï‚à™{‚àû} := inferInstanceAs (Countable <| Option _)

namespace InternalProjectivityProof

-- TODO: give things shorter names and document the proof better.

section

variable {X Y : LightProfinite} (y : Y) (f : X ‚ü∂ Y)

def fibre : LightProfinite :=
  haveI : CompactSpace (f ‚Åª¬π' {y}) :=
    isCompact_iff_compactSpace.mp (IsClosed.preimage (by fun_prop) isClosed_singleton).isCompact
  CompHausLike.of _ (f ‚Åª¬π' {y})

def fibre_incl : fibre y f ‚ü∂ X := ‚ü®{ toFun := Subtype.val }‚ü©

variable {Z : LightProfinite} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z}

end

def fibresOfOption {S T X : Type*} (œÄ : T ‚Üí S √ó Option X) (œÉ : Option X ‚Üí S ‚Üí T) : Set T :=
  {t : T | (œÄ t).2 = none} ‚à™ (‚ãÉ (x : X), Set.range (œÉ x))

@[simp, grind =]
lemma mem_fibresOfOption_iff {S T X : Type*} (œÄ : T ‚Üí S √ó Option X) (œÉ : Option X ‚Üí S ‚Üí T) (t : T) :
    t ‚àà fibresOfOption œÄ œÉ ‚Üî (œÄ t).2 = none ‚à® ‚àÉ (x : X) (s : S), œÉ x s = t := by
  simp [fibresOfOption]

lemma fibresOfOption_compl_eq_iUnion {S T X : Type*} (œÄ : T ‚Üí S √ó Option X) (œÉ : Option X ‚Üí S ‚Üí T)
    (hœÉ' : ‚àÄ (x : Option X) (s : S), (œÄ (œÉ x s)).2 = x) :
    (fibresOfOption œÄ œÉ)·∂ú =
      ‚ãÉ i, (Set.range (œÉ (Option.some i)))·∂ú ‚à© (Prod.snd ‚àò œÄ) ‚Åª¬π' {(i : OnePoint X)} := by
  ext x
  -- simp?:
  simp only [Set.mem_compl_iff, mem_fibresOfOption_iff, not_or, not_exists, Set.mem_iUnion,
    Set.mem_inter_iff, Set.mem_range, Set.mem_preimage, Function.comp_apply,
    Set.mem_singleton_iff]
  refine ‚ü®fun ‚ü®h‚ÇÅ, h‚ÇÇ‚ü© ‚Ü¶ ?_, fun ‚ü®n, hn, hn'‚ü© ‚Ü¶ ?_‚ü©
  ¬∑ obtain ‚ü®n, hn‚ü© := Option.ne_none_iff_exists'.mp h‚ÇÅ
    exact ‚ü®n, h‚ÇÇ n, hn‚ü©
  ¬∑ refine ‚ü®by simpa [hn'] using Option.isSome_iff_ne_none.mp rfl, fun i s h ‚Ü¶ hn s ?_‚ü©
    rw [‚Üê h, hœÉ'] at hn'
    rw [‚Üê h, Option.some_injective _ hn'.symm]

lemma fibresOfOption_closed {S T X : Type*} [TopologicalSpace S] [TopologicalSpace T]
    [TopologicalSpace X] [DiscreteTopology X] [T2Space T] [CompactSpace S]
    (œÄ : T ‚Üí S √ó OnePoint X) (hœÄ : Continuous œÄ)
    (œÉ : Option X ‚Üí S ‚Üí T) (hœÉ : ‚àÄ x, Continuous (œÉ x))
    (hœÉ' : ‚àÄ (x : Option X) (s : S), (œÄ (œÉ x s)).2 = x) :
    IsClosed (fibresOfOption œÄ œÉ) := IsClosed.mk <| by
  rw [fibresOfOption_compl_eq_iUnion œÄ œÉ hœÉ']
  refine isOpen_iUnion fun i ‚Ü¶ IsOpen.inter ?_ ?_
  ¬∑ simpa using IsCompact.isClosed (isCompact_range (hœÉ i))
  ¬∑ exact .preimage (continuous_snd.comp hœÄ) ‚ü®fun h ‚Ü¶ by simp_all, by simp‚ü©

def œÄ_r {S T X : Type*} (œÄ : T ‚Üí S √ó Option X) (œÉ : Option X ‚Üí S ‚Üí T) :
    fibresOfOption œÄ œÉ ‚Üí S √ó Option X :=
  fun x ‚Ü¶ œÄ x

@[grind =]
lemma œÄ_r_apply {S T X : Type*} (œÄ : T ‚Üí S √ó Option X) (œÉ : Option X ‚Üí S ‚Üí T)
    (x : fibresOfOption œÄ œÉ) : œÄ_r œÄ œÉ x = œÄ x :=
  rfl

def fibreInclGeneral {S T : Type*} (y : T) (f : S ‚Üí T) : f ‚Åª¬π' {y} ‚Üí S := fun x ‚Ü¶ x

def fibreIncl {S T X : Type*} (œÄ : T ‚Üí S √ó Option X) (œÉ : Option X ‚Üí S ‚Üí T) :
    (Prod.snd ‚àò œÄ_r œÄ œÉ) ‚Åª¬π' {none} ‚Üí fibresOfOption œÄ œÉ :=
  fibreInclGeneral none (Prod.snd ‚àò œÄ_r œÄ œÉ)

@[grind =]
lemma fibreIncl_apply {S T X : Type*} (œÄ : T ‚Üí S √ó Option X) (œÉ : Option X ‚Üí S ‚Üí T)
    (x : (Prod.snd ‚àò œÄ_r œÄ œÉ) ‚Åª¬π' {none}) : fibreIncl œÄ œÉ x = x :=
  rfl

lemma fibresOfOption_surjective {S T X : Type*}
    (œÄ : T ‚Üí S √ó Option X) (hœÄ : œÄ.Surjective) (œÉ : Option X ‚Üí S ‚Üí T)
    (hœÉ : ‚àÄ (x : Option X) s, (œÄ (œÉ x s)).1 = s)
    (hœÉ' : ‚àÄ (x : Option  X) (s : S), (œÄ (œÉ x s)).2 = x) :
    (fun (x : fibresOfOption œÄ œÉ) ‚Ü¶ œÄ x).Surjective := by
  rintro ‚ü®s, (rfl | x)‚ü©
  ¬∑ obtain ‚ü®y, hy‚ü© := hœÄ (s, none)
    exact ‚ü®‚ü®y, by grind‚ü©, hy‚ü©
  ¬∑ exact ‚ü®‚ü®œÉ x s, by simp‚ü©, Prod.ext (by grind) (by grind)‚ü©

def smartCoverToFun {S T X Y : Type*} (i : Y ‚Üí T) (œÄ : T ‚Üí S √ó Option X) :
    T ‚äï {xy : Y √ó Y // œÄ (i xy.1) = œÄ (i xy.2)} ‚Üí {xy : T √ó T // œÄ xy.1 = œÄ xy.2} :=
  Sum.elim (fun t ‚Ü¶ ‚ü®(t, t), rfl‚ü©) (fun xy ‚Ü¶ ‚ü®(i xy.val.1, i xy.val.2), xy.prop‚ü©)

@[grind =]
lemma smartCoverToFun_apply {S T X Y : Type*} (i : Y ‚Üí T) (œÄ : T ‚Üí S √ó Option X)
    (t : T ‚äï {xy : Y √ó Y // œÄ (i xy.1) = œÄ (i xy.2)}) :
    smartCoverToFun i œÄ t =
      Sum.elim (fun t ‚Ü¶ ‚ü®(t, t), rfl‚ü©) (fun xy ‚Ü¶ ‚ü®(i xy.val.1, i xy.val.2), xy.prop‚ü©) t :=
  rfl

lemma smartCoverToFun_surjective {S T X : Type*} (œÄ : T ‚Üí S √ó Option X) (œÉ : Option X ‚Üí S ‚Üí T)
    (hœÉ : ‚àÄ (x : Option X) s, (œÄ (œÉ x s)).1 = s) (hœÉ' : ‚àÄ (x : Option X) (s : S), (œÄ (œÉ x s)).2 = x) :
    Function.Surjective (smartCoverToFun (fibreIncl œÄ œÉ) (œÄ_r œÄ œÉ)) := by
  intro ‚ü®‚ü®‚ü®t, ht‚ü©, ‚ü®t', ht'‚ü©‚ü©, _‚ü©
  by_cases h : (œÄ t).2 = none
  ¬∑ exact ‚ü®Sum.inr ‚ü®(‚ü®‚ü®t, ht‚ü©, by grind‚ü©, ‚ü®‚ü®t', ht'‚ü©, by grind‚ü©), by grind‚ü©, by grind‚ü©
  ¬∑ obtain ‚ü®n, hn‚ü© := Option.ne_none_iff_exists'.mp h
    exact ‚ü®Sum.inl ‚ü®œÉ n (œÄ t).1, by grind‚ü©, by grind‚ü©

def smartCoverNew {S T : LightProfinite} (œÄ : T ‚ü∂ S ‚äó ‚Ñï‚à™{‚àû}) :
    (CompHausLike.of _ (T ‚äï (CompHausLike.pullback (fibre_incl ‚àû (œÄ ‚â´ snd S ‚Ñï‚à™{‚àû}) ‚â´ œÄ)
      (fibre_incl ‚àû (œÄ ‚â´ snd S ‚Ñï‚à™{‚àû}) ‚â´ œÄ)))) ‚ü∂ CompHausLike.pullback œÄ œÄ := ‚ü®{
  toFun := smartCoverToFun _ _
  continuous_toFun := by dsimp [smartCoverToFun]; fun_prop }‚ü©

def sectionOfFibreIncl {S T X : Type*} (œÄ : T ‚Üí S √ó Option X) (œÉ : Option X ‚Üí S ‚Üí T)
    (hœÉ' : ‚àÄ (x : Option X) (s : S), (œÄ (œÉ x s)).2 = x) : S ‚Üí (Prod.snd ‚àò œÄ_r œÄ œÉ) ‚Åª¬π' {none} :=
  fun s ‚Ü¶ ‚ü®‚ü®œÉ none s, by grind‚ü©, by grind‚ü©

def S' {S T X : Type*} (œÄ : T ‚Üí S √ó OnePoint X) :
    Set (‚àÄ x : OnePoint X, (Prod.snd ‚àò œÄ) ‚Åª¬π' {x}) :=
  {x | ‚àÄ n m, (œÄ (x n).val).1 = (œÄ (x m).val).1}

@[simp, grind =]
lemma mem_S'_iff {S T X : Type*} (œÄ : T ‚Üí S √ó OnePoint X)
    (y : ‚àÄ x : OnePoint X, (Prod.snd ‚àò œÄ) ‚Åª¬π' {x}) : y ‚àà S' œÄ ‚Üî
      ‚àÄ n m, (œÄ (y n).val).1 = (œÄ (y m).val).1 :=
  Iff.rfl

def y {S T X : Type*} (œÄ : T ‚Üí S √ó OnePoint X) : S' œÄ ‚Üí S :=
  fun x ‚Ü¶ (œÄ (x.val ‚àû).val).1

@[simp]
lemma y_apply {S T X : Type*} (œÄ : T ‚Üí S √ó OnePoint X) (x : S' œÄ) : y œÄ x = (œÄ (x.val ‚àû).val).1 :=
  rfl

lemma y_continuous {S T X : Type*} [TopologicalSpace S] [TopologicalSpace T]
    [TopologicalSpace X] (œÄ : T ‚Üí S √ó OnePoint X) (hœÄ : Continuous œÄ := by fun_prop) :
    Continuous (y œÄ) :=
  continuous_fst.comp <| hœÄ.comp <| continuous_subtype_val.comp <|
    (continuous_apply _).comp (by fun_prop)

lemma S'_compactSpace {S T X : Type*} [TopologicalSpace S] [T2Space S] [TopologicalSpace T]
    [CompactSpace T] [TopologicalSpace X] [T1Space (OnePoint X)]
    (œÄ : T ‚Üí S √ó OnePoint X) (hœÄ : Continuous œÄ) : CompactSpace (S' œÄ) := by
  rw [‚Üê isCompact_iff_compactSpace, show S' œÄ =
    ‚ãÇ (n : OnePoint X) (m : OnePoint X), {x | (œÄ (x n).val).1 = (œÄ (x m).val).1} by aesop]
  have (x : OnePoint X) : CompactSpace <| (Prod.snd ‚àò œÄ) ‚Åª¬π' {x} :=
    isCompact_iff_compactSpace.mp (IsClosed.preimage (by fun_prop) isClosed_singleton).isCompact
  refine (isClosed_iInter fun n ‚Ü¶ isClosed_iInter fun m ‚Ü¶ isClosed_eq ?_ ?_).isCompact
  all_goals fun_prop

@[simps! pt Œπ_app]
noncomputable def c {X : LightCondMod R} {S T : LightProfinite} (œÄ : T ‚ü∂ (S ‚äó ‚Ñï‚à™{‚àû}))
    [Epi ((lightProfiniteToLightCondSet ‚ãô (free R)).map <| smartCoverNew œÄ)]
    (g : ((lightProfiniteToLightCondSet ‚ãô free R).obj T) ‚ü∂ X)
    (r_inf : T ‚ü∂ (fibre ‚àû (œÄ ‚â´ snd _ _))) (œÉ : S ‚ü∂ (fibre ‚àû (œÄ ‚â´ snd _ _)))
    (hr : fibre_incl ‚àû (œÄ ‚â´ snd _ _) ‚â´ r_inf = ùüô (fibre ‚àû (œÄ ‚â´ snd _ _))) :
    Cocone ((parallelPair (lightProfiniteToLightCondSet.map (CompHausLike.pullback.fst œÄ œÄ))
      (lightProfiniteToLightCondSet.map (CompHausLike.pullback.snd œÄ œÄ))) ‚ãô (free R)) := by
  refine Cocone.ofCofork (Cofork.ofœÄ (g -
    (lightProfiniteToLightCondSet ‚ãô (free R)).map (r_inf ‚â´ fibre_incl ‚àû (œÄ ‚â´ snd _ _)) ‚â´ g +
    (lightProfiniteToLightCondSet ‚ãô (free R)).map
      (r_inf ‚â´ fibre_incl ‚àû (œÄ ‚â´ snd _ _) ‚â´ œÄ ‚â´ fst _ _ ‚â´ œÉ ‚â´ fibre_incl ‚àû (œÄ ‚â´ snd _ _)) ‚â´ g) ?_)
  rw [‚Üê cancel_epi ((lightProfiniteToLightCondSet ‚ãô (free R)).map <| smartCoverNew œÄ)]
  apply (isColimitOfPreserves (lightProfiniteToLightCondSet ‚ãô (free R))
      (CompHausLike.coproductIsColimit _ _)).hom_ext
  rintro ‚ü®‚ü®‚ü©‚ü©
  ¬∑ simp [‚Üê Functor.map_comp_assoc, -Functor.map_comp]
    rfl
  ¬∑ -- simp? [‚Üê map_comp_assoc, -Functor.map_comp]:
    simp only [comp_obj, pair_obj_right, mapCocone_pt, const_obj_obj, mapCocone_Œπ_app,
      Functor.comp_map, parallelPair_obj_zero, parallelPair_obj_one, parallelPair_map_left,
      Preadditive.comp_add, Preadditive.comp_sub, ‚Üê map_comp_assoc, parallelPair_map_right]
    have : smartCoverNew œÄ = (BinaryCofan.IsColimit.desc' (CompHausLike.coproductIsColimit _ _)
        (CompHausLike.pullback.lift _ _ (ùüô T) (ùüô T) (by simp))
        (CompHausLike.pullback.lift _ _ ((CompHausLike.pullback.fst _ _) ‚â´ fibre_incl _ _)
          ((CompHausLike.pullback.snd _ _) ‚â´ fibre_incl _ _)
          (by simp [CompHausLike.pullback.condition]))).val := rfl
    -- simp? [this, ‚Üê Functor.map_comp]:
    simp only [this, pair_obj_left, const_obj_obj, pair_obj_right, BinaryCofan.IsColimit.desc'_coe,
      IsColimit.fac, BinaryCofan.mk_pt, BinaryCofan.mk_inr, ‚Üê Functor.map_comp,
      CompHausLike.pullback.lift_fst, IsColimit.fac_assoc, Category.assoc,
      CompHausLike.pullback.lift_snd]
    -- simp? [-Functor.map_comp, ‚Üê Category.assoc, hr]:
    simp only [‚Üê Category.assoc, hr, Category.id_comp, sub_self, zero_add]
    simp [CompHausLike.pullback.condition]

instance (X Y : LightProfinite.{u}) [Nonempty X] : Epi (snd X Y) := by
  rw [LightProfinite.epi_iff_surjective]
  exact fun y ‚Ü¶ ‚ü®‚ü®Nonempty.some inferInstance, y‚ü©, rfl‚ü©

lemma aux {S T : LightProfinite} (œÄ : T ‚ü∂ S ‚äó ‚Ñï‚à™{‚àû}) [Epi œÄ] :
    ‚àÉ (S' T' : LightProfinite) (y' : S' ‚ü∂ S) (œÄ' : T' ‚ü∂ S' ‚äó ‚Ñï‚à™{‚àû}) (g' : T' ‚ü∂ T),
      Epi œÄ' ‚àß Epi y' ‚àß œÄ' ‚â´ (y' ‚ñ∑ ‚Ñï‚à™{‚àû}) = g' ‚â´ œÄ ‚àß
        IsSplitEpi (fibre_incl ‚àû (œÄ' ‚â´ snd S' ‚Ñï‚à™{‚àû}) ‚â´ œÄ' ‚â´ fst S' ‚Ñï‚à™{‚àû}) ‚àß
          Epi (smartCoverNew œÄ') := by
  have := S'_compactSpace œÄ (by fun_prop)
  let S'œÄ (n : ‚Ñï‚à™{‚àû}) : LightProfinite.of (S' œÄ) ‚ü∂ fibre n (œÄ ‚â´ snd _ _) :=
    ‚ü®{ toFun x := x.val n, continuous_toFun := by refine (continuous_apply _).comp ?_; fun_prop }‚ü©
  let y' : LightProfinite.of (S' œÄ) ‚ü∂ S := ConcreteCategory.ofHom ‚ü®y œÄ, y_continuous œÄ‚ü©
  let œÄ' := CompHausLike.pullback.snd œÄ (y' ‚ñ∑ ‚Ñï‚à™{‚àû})
  let œÉ' : ‚Ñï‚à™{‚àû} ‚Üí LightProfinite.of (S' œÄ) ‚Üí CompHausLike.pullback œÄ (y' ‚ñ∑ ‚Ñï‚à™{‚àû}) :=
    fun n ‚Ü¶ CompHausLike.pullback.lift _ _
      (S'œÄ n ‚â´ fibre_incl _ _) (lift (ùüô _) (CompHausLike.const _ n)) <| by
        apply CartesianMonoidalCategory.hom_ext
        ¬∑ ext x; exact x.prop n ‚àû
        ¬∑ ext x; exact (x.val n).prop
  have hœÉ (x : ‚Ñï‚à™{‚àû}) (s : LightProfinite.of (S' œÄ)) : (œÄ' (œÉ' x s)).1 = s := rfl
  have hœÉ' (x : ‚Ñï‚à™{‚àû}) (s : LightProfinite.of (S' œÄ)) : (œÄ' (œÉ' x s)).2 = x := rfl
  have : CompactSpace (fibresOfOption œÄ' œÉ') := isCompact_iff_compactSpace.mp
    (fibresOfOption_closed œÄ' (by fun_prop) œÉ' (by fun_prop) hœÉ').isCompact
  refine ‚ü®LightProfinite.of (S' œÄ), LightProfinite.of (fibresOfOption œÄ' œÉ'), y',
    ‚ü®‚ü®Subtype.val, by fun_prop‚ü©‚ü© ‚â´ œÄ',
    ‚ü®‚ü®Subtype.val, by fun_prop‚ü©‚ü© ‚â´ CompHausLike.pullback.fst _ _, ?_, ?_, ?_, ?_, ?_‚ü©
  ¬∑ rw [LightProfinite.epi_iff_surjective]
    refine fibresOfOption_surjective _ ?_ _ hœÉ hœÉ'
    rw [‚Üê LightProfinite.epi_iff_surjective]
    dsimp [œÄ']
    infer_instance
  ¬∑ rw [LightProfinite.epi_iff_surjective]
    intro y
    have : Function.Surjective œÄ := by rwa [‚Üê LightProfinite.epi_iff_surjective]
    let p (s : S) (n : ‚Ñï‚à™{‚àû}) : T := (this (s, n)).choose
    have hp (s : S) (n : ‚Ñï‚à™{‚àû}) : œÄ (p s n) = (s, n) := (this (s, n)).choose_spec
    exact ‚ü®‚ü®fun n ‚Ü¶ ‚ü®p y n, by grind‚ü©, by grind‚ü©, by simp [y', hp]‚ü©
  ¬∑ simp [œÄ', CompHausLike.pullback.condition]
  ¬∑ exact ‚ü®ConcreteCategory.ofHom ‚ü®(sectionOfFibreIncl œÄ' œÉ' hœÉ'),
      (.subtype_mk (.subtype_mk (by fun_prop) _) _)‚ü©, rfl‚ü©
  ¬∑ rw [LightProfinite.epi_iff_surjective]
    exact smartCoverToFun_surjective _ _ hœÉ hœÉ'

end InternalProjectivityProof

open InternalProjectivityProof

theorem LightCondensed.internallyProjective_free_natUnionInfty :
    InternallyProjective ((free R).obj (‚Ñï‚à™{‚àû}).toCondensed) := by
  rw [free_lightProfinite_internallyProjective_iff_tensor_condition' R ‚Ñï‚à™{‚àû}]
  intro X Y p hp S f
  obtain ‚ü®T, œÄ, g, hœÄ, comm‚ü© := comm_sq R p f
  obtain ‚ü®S', T', y', œÄ', g', hœÄ', hy', comp, ‚ü®‚ü®split‚ü©‚ü©, epi‚ü© := aux œÄ
  refine ‚ü®S', y', ?_‚ü©
  by_cases hS' : Nonempty S'
  ¬∑ have : Mono (fibre_incl ‚àû (œÄ' ‚â´ snd _ _)) := by
      rw [CompHausLike.mono_iff_injective]
      simp [fibre_incl]
      exact Subtype.val_injective
    have : Nonempty (fibre ‚àû (œÄ' ‚â´ snd _ _)) := by
      have : Epi (œÄ' ‚â´ snd S' ‚Ñï‚à™{‚àû}) := inferInstance
      obtain ‚ü®x, hx‚ü© := (LightProfinite.epi_iff_surjective _).mp this ‚àû
      refine ‚ü®x, by simpa using hx‚ü©
    obtain ‚ü®r_inf, hr‚ü© := Injective.factors (ùüô _) (fibre_incl ‚àû (œÄ' ‚â´ snd _ _))
    have hc := isColimitOfPreserves (free R) (explicitRegularIsColimit œÄ')
    refine ‚ü®(LightProfinite.epi_iff_surjective _).mp inferInstance,
      hc.desc (c R œÄ' ((lightProfiniteToLightCondSet ‚ãô (free R)).map g' ‚â´ g)
      r_inf split.section_ hr), ?_‚ü©
    rw [‚Üê cancel_epi ((lightProfiniteToLightCondSet ‚ãô (free R)).map œÄ'),
      ‚Üê Functor.comp_map, ‚Üê Functor.map_comp_assoc]
    change _ = (((free R).mapCocone _).Œπ.app one ‚â´ hc.desc (c R œÄ' _ r_inf split.section_ hr)) ‚â´ p
    rw [hc.fac]
    -- simp? [‚Üê comm]:
    simp only [comp_obj, parallelPair_obj_one, Functor.comp_map, Functor.map_comp, Category.assoc,
      c_pt, c_Œπ_app, eqToHom_refl, Preadditive.comp_add, Preadditive.comp_sub, Category.id_comp,
      Preadditive.add_comp, Preadditive.sub_comp, ‚Üê comm]
    simp only [‚Üê Functor.map_comp, ‚Üê Functor.comp_map, ‚Üê Category.assoc, ‚Üê comp]
    symm
    rw [sub_add, sub_eq_self, sub_eq_zero]
    simp only [Category.assoc]
    have : fibre_incl ‚àû (œÄ' ‚â´ snd _ _) ‚â´ œÄ' = fibre_incl ‚àû (œÄ' ‚â´ snd _ _) ‚â´ œÄ' ‚â´ fst _ _ ‚â´
        lift (ùüô _) (CompHausLike.const S' (‚àû : ‚Ñï‚à™{‚àû})) := by
      apply CartesianMonoidalCategory.hom_ext
      ¬∑ simp [fibre_incl]
      ¬∑ ext a
        exact a.prop
    rw [reassoc_of% this, reassoc_of% split.id]
  ¬∑ have hh : IsEmpty (S' ‚äó ‚Ñï‚à™{‚àû}) := isEmpty_prod.mpr <| Or.inl <| by simpa using hS'
    have : IsIso œÄ' := ‚ü®ConcreteCategory.ofHom ‚ü®(hh.elim ¬∑), continuous_of_const <| by aesop‚ü©,
      by ext x; exact hh.elim (œÄ' x), by ext x; all_goals exact hh.elim x‚ü©
    refine ‚ü®(LightProfinite.epi_iff_surjective _).mp inferInstance,
      (lightProfiniteToLightCondSet ‚ãô (free R)).map (inv œÄ' ‚â´ g') ‚â´ g, ?_‚ü©
    -- simp? [‚Üê comm, ‚Üê cancel_epi ((lightProfiniteToLightCondSet ‚ãô (free R)).map œÄ')]:
    simp only [comp_obj, Functor.comp_map, Functor.map_comp, Functor.map_inv, Category.assoc,
      ‚Üê comm, ‚Üê cancel_epi ((lightProfiniteToLightCondSet ‚ãô (free R)).map œÄ'),
      IsIso.hom_inv_id_assoc]
    simp [‚Üê Category.assoc, ‚Üê Functor.map_comp, ‚Üê comp]
